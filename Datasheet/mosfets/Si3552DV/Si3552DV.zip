*Jan 2, 2006
*Doc. ID: 75019, S-52634, Rev. C
*Dn Gn Sn Dp Gp Sp
.SUBCKT Si3552DV 6 2 1 3 5 4  
X1 6 2 1 Si3552N
X2 3 5 4 Si3552P
.ENDS Si3552DV 
*N-Ch
.SUBCKT Si3552N 4 1 2    
M1  3 1 2 2 NMOS W=137500u L=0.50u 
M2  2 1 2 4 PMOS W=137500u L=0.90u  
R1  4 3     RTEMP 500E-4
CGS 1 2     158E-12
DBD 2 4     DBD
**************************************************************
.MODEL  NMOS       NMOS  (LEVEL  = 3          TOX    = 5E-8
+ RS     = 250E-4         RD     = 0          NSUB   = 1.65E17  
+ KP     = 3.25E-5        UO     = 650             
+ VMAX   = 0              XJ     = 5E-7       KAPPA  = 60E-3
+ ETA    = 1E-4           TPG    = 1  
+ IS     = 0              LD     = 0                           
+ CGSO   = 0              CGDO   = 0          CGBO   = 0 
+ TLEV   = 1              BEX    = -1.5       TCV    = 4E-3
+ NFS    = 0.8E12         DELTA  = 0.1)
**************************************************************
.MODEL  PMOS       PMOS (LEVEL   = 3          TOX    = 5E-8
+NSUB    = 4.3E16         TPG    = -1)   
**************************************************************
.MODEL DBD D (CJO=85E-12 VJ=0.38 M=0.32
+RS=0.5  FC=0.5 IS=1E-12 TT=5E-8 N=1 BV=31)
**************************************************************
.MODEL RTEMP R (TC1=6.7E-3 TC2=5.5E-6)
**************************************************************
.ENDS Si3552N
*P-Ch
.SUBCKT Si3552P 4 1 2  
M1  3 1 2 2 PMOS W=137500u L=0.50u 
M2  2 1 2 4 NMOS W=137500u L=1.94u 
R1  4 3     RTEMP 55E-3
CGS 1 2     145E-12
DBD 4 2     DBD
*****************************************************************
.MODEL  PMOS       PMOS (  LEVEL  = 3             TOX    = 5E-8
+ RS     = 89E-3           RD     = 0             NSUB   = 0.7E17   
+ kp     = 1.5E-5          UO     = 400             
+ VMAX   = 0               XJ     = 5E-7          KAPPA  = 10E-2
+ ETA    = 1E-4            TPG    = -1  
+ IS     = 0               LD     = 0                        
+ CGSO   = 0               CGDO   = 0             CGBO   = 0 
+ TLEV   = 1               BEX    = -1.5          TCV    = 3.9E-3
+ NFS    = 0.8E12          DELTA  = 0.1)
*****************************************************************
.MODEL  NMOS       NMOS (  LEVEL  = 3             TOX    = 5E-8
+NSUB    = 15E15            TPG    = 1)   
*****************************************************************
.MODEL DBD D (CJO=85E-12 VJ=0.38 M=0.34
+RS=5 FC=0.5 IS=1E-12 TT=5E-8 N=1 BV=35)
*****************************************************************
.MODEL RTEMP R (TC1=10E-3 TC2=5.5E-6)
*****************************************************************
.ENDS Si3552P