*Nov 21, 2005
*Doc. ID: 77274, S-52398, Rev. B
.SUBCKT Si8413DB 4 1 2
M1  3 1 2 2 PMOS W=1315616u L=0.25u          
M2  2 1 2 4 NMOS W=1315616u L=0.40u
R1  4 3     RTEMP 20E-3
CGS 1 2     650E-12
DBD 4 2     DBD
***************************************************************  
.MODEL  PMOS       PMOS  ( LEVEL  = 3           TOX    = 3E-8
+ RS     = 8E-3            RD     = 0           NSUB   = 2.7E16
+ KP     = 5.7E-6          UO     = 400             
+ VMAX   = 0               XJ     = 5E-7        KAPPA  = 1E-3
+ ETA    = 1E-4            TPG    = -1  
+ IS     = 0               LD     = 0                        
+ CGSO   = 0               CGDO   = 0           CGBO   = 0 
+ TLEV   = 1               BEX    = -1.5        TCV    = 3.3E-3
+ NFS    = 0.8E12          DELTA  = 0.1)
*************************************************************** 
.MODEL  NMOS       NMOS  ( LEVEL  = 3           TOX    = 3E-8
+NSUB    = 3E17            TPG    = -1)   
*************************************************************** 
.MODEL DBD D (CJO=250E-12 VJ=0.38 M=0.35
+RS=0.1 FC=0.5 IS=1E-12 TT=6E-8 N=1 BV=20.2)
*************************************************************** 
.MODEL RTEMP R (TC1=4E-3 TC2=5.5E-6)
*************************************************************** 
.ENDS