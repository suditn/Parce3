*May 21, 2007
*Doc. ID: 74728, S-71047, Rev. A
*File Name: Si5504BDC_PS.txt and Si5504BDC_PS.lib
*This document is intended as a SPICE modeling guideline and does not
*constitute a commercial product data sheet.  Designers should refer to the
*appropriate data sheet of the same number for guaranteed specification
*limits.
.SUBCKT Si5504BDC D1 G1 S1 D2 G2 S2 
X1 D1 G1 S1 Si5504N
X2 D2 G2 S2 Si5504P
.ENDS Si5504BDC
*N-CH
.SUBCKT Si5504N D G S
M1  3  GX S S NMOS W=293675u L=0.25u  
M2  S  GX S D PMOS W=293675u L=0.35u  
RG  G  GX     3
R1  D  3      RTEMP 1.5E-2
CGS GX S      170E-12
DBD S  D      DBD
**************************************************************** 
.MODEL  NMOS        NMOS ( LEVEL  = 3           TOX    = 5E-8
+ RS     = 2.9E-2          RD     = 0           NSUB   = 2.6E17   
+ kp     = 1.2E-5          UO     = 650             
+ VMAX   = 0               XJ     = 5E-7        KAPPA  = 4E-2
+ ETA    = 1E-4            TPG    = 1  
+ IS     = 0               LD     = 0                             
+ CGSO   = 0               CGDO   = 0           CGBO   = 0 
+ NFS    = 0.8E12          DELTA  = 0.1)
**************************************************************** 
.MODEL  PMOS        PMOS ( LEVEL  = 3           TOX    = 5E-8
+NSUB    = 5E16            IS     = 0           TPG    = -1)   
**************************************************************** 
.MODEL DBD D (CJO=55E-12 VJ=0.38 M=0.17   
+FC=0.5 TT=1.42E-8 T_MEASURED=25 BV=31 
+RS=1.399e-02 N=1.341 IS=1.781e-10 IKF=1000
+EG=1.073 XTI=5.172 TRS1=3.070e-03 )
**************************************************************** 
.MODEL RTEMP RES (TC1=12E-3 TC2=5.5E-6)
**************************************************************** 
.ENDS Si5504N
*P-CH
.SUBCKT Si5504P D G S
M1  3  GX S S PMOS W=273593u L=0.25u             
M2  S  GX S D NMOS W=273593u L=0.50u 
RG  G  GX     13
R1  D  3      RTEMP 4.5E-2
CGS GX S      110E-12
DBD D  S      DBD
***************************************************************  
.MODEL  PMOS        PMOS ( LEVEL  = 3          TOX    = 5E-8
+ RS     = 4.2E-2          RD     = 0          NSUB   = 1.2E17
+ KP     = 5E-6            UO     = 400             
+ VMAX   = 0               XJ     = 5E-7       KAPPA  = 4E-3
+ ETA    = 1E-4            TPG    = -1  
+ IS     = 0               LD     = 0                        
+ CGSO   = 0               CGDO   = 0          CGBO   = 0 
+ NFS    = 0.8E12          DELTA  = 0.1)
*************************************************************** 
.MODEL  NMOS        NMOS ( LEVEL  = 3          TOX    = 5E-8
+NSUB    = 9E16            IS     = 0          TPG    = -1)   
*************************************************************** 
.MODEL DBD D (CJO=50E-12 VJ=0.38 M=0.30
+FC=0.5 TT=1.06e-08 T_MEASURED=25 BV=31 
+RS=1.396e-02 N=1.463 IS=6.693e-10 IKF=1000
+EG=1.14 XTI=3.62 TRS1=2.978e-03 )
*************************************************************** 
.MODEL RTEMP RES (TC1=9E-3 TC2=5.5E-6)
*************************************************************** 
.ENDS Si5504P
