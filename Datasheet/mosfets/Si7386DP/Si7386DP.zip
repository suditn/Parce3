*Dec 12, 2005
*Doc. ID: 77467, S-52522, Rev. A
.SUBCKT Si7386DP D G S
M1  3 G S S NMOS W=3453247u L=0.25u       
M2  S G S D PMOS W=3453247u L=0.25u  
R1  D 3     RTEMP 2E-3
CGS G S     900E-12
DBD S D     DBD
****************************************************************  
.MODEL  NMOS        NMOS ( LEVEL  = 3           TOX    = 5E-8
+ RS     = 3E-3            RD     = 0		NSUB   = 2.77E17      
+ KP     = 1.5E-5          UO     = 650 	
+ VMAX   = 0               XJ     = 5E-7        KAPPA  = 1E-2
+ ETA    = 1E-4            TPG    = 1  
+ IS     = 0               LD     = 0                             
+ CGSO   = 0               CGDO   = 0           CGBO   = 0 
+ TLEV   = 1               BEX    = -1.5        TCV    = 4.6E-3
+ NFS    = 0.8E12          DELTA  = 0.1)
**************************************************************** 
.MODEL  PMOS        PMOS ( LEVEL  = 3           TOX    = 5E-8
+NSUB    = 3E16            TPG    = -1)           
 *************************************************************** 
.MODEL DBD D (CJO=700E-12 VJ=0.38 M=0.30
+RS=0.1 FC=0.1 IS=1E-12 TT=5E-8 N=1 BV=20.2)
**************************************************************** 
.MODEL RTEMP R (TC1=6E-3 TC2=5.5E-6)
**************************************************************** 
.ENDS