*March 21, 2005
*Doc. ID: 76377, S-50383, Rev. B
.SUBCKT Si2315BDS  4 1 2
M1  3 1 2 2 PMOS W=588669u L=0.50u     
M2  2 1 2 4 NMOS W=588669u L=0.45u  
R1  4 3     RTEMP 12E-3
CGS 1 2     100E-12
DBD 4 2     DBD
*****************************************************************
.MODEL  PMOS       PMOS (  LEVEL  = 3             TOX    = 1.7E-8
+ RS     = 11E-3           RD     = 0             NSUB   = 2E15   
+ KP     = 1.9E-5          XJ     = 5E-7          KAPPA  = 2E-3
+ ETA    = 1E-4            TPG    = -1  
+ IS     = 0               LD     = 0                  
+ CGSO   = 0               CGDO   = 0             CGBO   = 0 
+ TLEV   = 1               BEX    = -1.5          TCV    = 1.8E-3
+ NFS    = 0.8E12	   DELTA  = 0.1)
*****************************************************************
.MODEL  NMOS       NMOS (  LEVEL  = 3             TOX    = 1.7E-8
+NSUB    = 5E17            TPG    = 1)   
*****************************************************************
.MODEL DBD D (CJO=400E-12 VJ=0.38 M=0.36
+RS=0.1 FC=0.5 IS=1E-12 TT=6.7E-8 N=1 BV=12.2)
*****************************************************************
.MODEL RTEMP R (TC1=4E-3 TC2=5.5E-6)
*****************************************************************
.ENDS