*June 13, 2005
*Doc. ID: 77757, S-51132, Rev. A
.SUBCKT Si4946BEY 4 1 2
M1  3 1 2 2 NMOS W=1368380u L=0.25u
M2  2 1 2 4 PMOS W=1368380u L=0.25u  
R1  4 3     RTEMP 14E-3
CGS 1 2     530E-12
DBD 2 4     DBD
************************************************************  
.MODEL  NMOS         NMOS (LEVEL  = 3        TOX    = 3E-8
+ RS     = 17E-3           RD     = 0        NSUB   = 5.72E17  
+ KP     = 9.5E-6          UO     = 650             
+ VMAX   = 0               XJ     = 5E-7     KAPPA  = 1E-1
+ ETA    = 1E-4            TPG    = 1  
+ IS     = 0               LD     = 0                             
+ CGSO   = 0               CGDO   = 0        CGBO   = 0 
+ TLEV   = 1               BEX    = -1.5     TCV    = 4E-3
+ NFS    = 0.8E12          DELTA  = 0.1)
************************************************************  
.MODEL  PMOS         PMOS (LEVEL  = 3        TOX    = 3E-8
+NSUB    = 4E16            TPG    = -1)   
************************************************************  
.MODEL DBD D (CJO=140E-12 VJ=0.38 M=0.27
+RS=0.01 FC=0.1 IS=1E-12 TT=2.8E-8 N=1 BV=60.2)
************************************************************ 
.MODEL RTEMP R (TC1=10E-3 TC2=5.5E-6)
************************************************************  
.ENDS
 
